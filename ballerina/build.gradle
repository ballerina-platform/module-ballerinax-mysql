/*
 * Copyright (c) 2020, WSO2 Inc. (http://www.wso2.org) All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

buildscript {
    repositories {
        maven {
            url = 'https://maven.pkg.github.com/ballerina-platform/plugin-gradle'
            credentials {
                username System.getenv("packageUser")
                password System.getenv("packagePAT")
            }
        }
    }
    dependencies {
        classpath "io.ballerina:plugin-gradle:${project.ballerinaGradlePluginVersion}"
    }
}

description = 'Ballerina - MySQL Ballerina Generator'

def packageName = 'mysql'
def packageOrg = 'ballerinax'
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")

def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/Ballerina.toml")
def compilerPluginTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/CompilerPlugin.toml")
def ballerinaTomlFile = new File("$project.projectDir/Ballerina.toml")
def compilerPluginTomlFile = new File("$project.projectDir/CompilerPlugin.toml")

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-')
        if (splitVersion.length > 3) {
            def strippedValues = splitVersion[0..-4]
            return strippedValues.join('-')
        } else {
            return extVersion
        }
    } else {
        return extVersion.replace("${project.ext.snapshotVersion}", '')
    }
}

apply plugin: 'io.ballerina.plugin'

ballerina {
    packageOrganization = packageOrg
    module = packageName
    langVersion = ballerinaLangVersion
}

dependencies {
    externalJars(group: 'io.ballerina.stdlib', name: 'sql-native', version: "${stdlibSqlVersion}") {
        transitive = false
    }
}

task updateTomlFiles {
    doLast {
        def stdlibDependentSqlVersion = stripBallerinaExtensionVersion(project.stdlibSqlVersion)

        def newConfig = ballerinaTomlFilePlaceHolder.text.replace('@project.version@', project.version.toString())
        newConfig = newConfig.replace('@toml.version@', tomlVersion)
        newConfig = newConfig.replace('@sql.version@', stdlibDependentSqlVersion)
        newConfig = newConfig.replace('@sql.native.version@', project.stdlibSqlVersion)
        newConfig = newConfig.replace('@stdlib.sql.version@', stdlibDependentSqlVersion)
        ballerinaTomlFile.text = newConfig

        def newCompilerPluginToml = compilerPluginTomlFilePlaceHolder.text.replace("@project.version@", project.version)
        compilerPluginTomlFile.text = newCompilerPluginToml
    }
}

task commitTomlFiles {
    doLast {
        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the native jar versions\" Ballerina.toml Dependencies.toml CompilerPlugin.toml"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' Ballerina.toml Dependencies.toml CompilerPlugin.toml"
            }
        }
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            artifact source: createArtifactZip, extension: 'zip'
        }
    }
    repositories {
        maven {
            name = 'GitHubPackages'
            url = uri("https://maven.pkg.github.com/ballerina-platform/module-${packageOrg}-${packageName}")
            credentials {
                username = System.getenv('publishUser')
                password = System.getenv('publishPAT')
            }
        }
    }
}

def checkExecResult(executionResult, failText, standardOutput) {
    if (executionResult) {
        if (executionResult.get().getExitValue() != 0) {
            throw new GradleException('Non-zero exit value: ' + executionResult.get().getExitValue())
        }
        if (standardOutput.toString().contains(failText)) {
            throw new GradleException('"' + failText + '" string in output: ' + standardOutput.toString())
        }
    } else {
        throw new GradleException('Returned a null executionResult object')
    }
}

task createTestDockerImage(type: Exec) {
    def standardOutput = new ByteArrayOutputStream()
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', "docker build -f %PROJECT_DIR%\\tests\\resources\\Dockerfile -t ballerina-mysql -q %PROJECT_DIR%\\tests\\resources\\"
        environment "PROJECT_DIR", project.projectDir
    } else {
        commandLine 'sh', '-c', "docker build -f $project.projectDir/tests/resources/Dockerfile -t ballerina-mysql -q $project.projectDir/tests/resources/"
    }
    doLast {
        checkExecResult(executionResult, 'Error', standardOutput)
        sleep(10 * 1000)
    }
}

def checkTestDockerContainerStatus(dockerName) {
    def cmd = "docker exec ${dockerName} mysqladmin ping -hlocalhost -uroot -pTest123# --silent"
    try {
        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            return exec {
                commandLine 'cmd', '/c', cmd
            }.exitValue
        } else {
            return exec {
                commandLine 'sh', '-c', cmd
            }.exitValue
        }
    } catch (all) {
        return 1
    }
}

task startTestDockerContainer(type: Exec) {
    def standardOutput = new ByteArrayOutputStream()
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        def dockerCmd = 'docker run --rm -d --name ballerina-mysql -p 3305:3306 -t ballerina-mysql'
        commandLine 'cmd', '/c', dockerCmd
        environment "PROJECT_DIR", project.projectDir
    } else {
        def dockerCmd = "docker run --rm -d --name ballerina-mysql -p 3305:3306 -t ballerina-mysql"
        commandLine 'sh', '-c', dockerCmd
    }
    def healthCheck = 1
    def counter = 0
    doLast {
        checkExecResult(executionResult, 'Error', standardOutput)
        while (healthCheck != 0 && counter < 12) {
            sleep(5 * 1000)
            healthCheck = checkTestDockerContainerStatus('ballerina-mysql')
            counter = counter + 1
        }
        if (healthCheck != 0) {
            throw new GradleException("Docker container 'ballerina-mysql' health test exceeded timeout!")
        }
    }
}

task startTestLimitedDockerContainer(type: Exec) {
    def standardOutput = new ByteArrayOutputStream()
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        def dockerCmd = 'docker run --rm -d --name ballerina-mysql-limited -p 3303:3306 -t ballerina-mysql'
        commandLine 'cmd', '/c', dockerCmd
        environment "PROJECT_DIR", project.projectDir
    } else {
        commandLine 'sh', '-c', 'docker run --rm -d --name ballerina-mysql-limited -p 3303:3306 -t ballerina-mysql'
    }
    def healthCheck = 1
    def counter = 0
    doLast {
        checkExecResult(executionResult, 'Error', standardOutput)
        while (healthCheck != 0 && counter < 12) {
            sleep(5 * 1000)
            healthCheck = checkTestDockerContainerStatus('ballerina-mysql-limited')
            counter = counter + 1
        }
        if (healthCheck != 0) {
            throw new GradleException("Docker container 'ballerina-mysql-limited' health test exceeded timeout!")
        }
    }
}

task startTestDockerTrxContainer(type: Exec) {
    def standardOutput = new ByteArrayOutputStream()
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        def dockerCmd = 'docker run --rm -d --name ballerina-mysql-trx -p 3304:3306 -t ballerina-mysql'
        commandLine 'cmd', '/c', dockerCmd
        environment "PROJECT_DIR", project.projectDir
    } else {
        commandLine 'sh', '-c', 'docker run --rm -d --name ballerina-mysql-trx -p 3304:3306 -t ballerina-mysql'
    }
    def healthCheck = 1
    def counter = 0
    doLast {
        checkExecResult(executionResult, 'Error', standardOutput)
        while (healthCheck != 0 && counter < 12) {
            sleep(5 * 1000)
            healthCheck = checkTestDockerContainerStatus('ballerina-mysql-trx')
            counter = counter + 1
        }
        if (healthCheck != 0) {
            throw new GradleException("Docker container 'ballerina-mysql-trx' health test exceeded timeout!")
        }
    }
}

task startTestSslDockerContainer(type: Exec) {
    def standardOutput = new ByteArrayOutputStream()
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        def dockerCmd = 'docker run --rm -d --name ballerina-mysql-ssl ' +
                '-v %PROJECT_DIR%\\tests\\resources\\mysql-ssl.cnf:/etc/mysql/conf.d/mysql-ssl.cnf ' +
                '-p 3307:3306 ' +
                '-t ballerina-mysql'
        commandLine 'cmd', '/c', dockerCmd
        environment "PROJECT_DIR", project.projectDir
    } else {
        commandLine 'sh', '-c', 'docker run --rm -d --name ballerina-mysql-ssl ' +
                "-v $project.projectDir/tests/resources/mysql-ssl.cnf:/etc/mysql/conf.d/mysql-ssl.cnf " +
                '-p 3307:3306 ' +
                '-t ballerina-mysql'
    }
    def healthCheck = 1
    def counter = 0
    doLast {
        checkExecResult(executionResult, 'Error', standardOutput)
        while (healthCheck != 0 && counter < 12) {
            sleep(5 * 1000)
            healthCheck = checkTestDockerContainerStatus('ballerina-mysql-ssl')
            counter = counter + 1
        }
        if (healthCheck != 0) {
            throw new GradleException("Docker container 'ballerina-mysql-ssl' health test exceeded timeout!")
        }
    }
}

task stopTestDockerContainer() {
    doLast {
        try {
            def stdOut = new ByteArrayOutputStream()
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                exec {
                    commandLine 'cmd', '/c', 'docker stop ballerina-mysql'
                    standardOutput = stdOut
                }
            } else {
                exec {
                    commandLine 'sh', '-c', 'docker stop ballerina-mysql'
                    standardOutput = stdOut
                }
            }
        } catch (all) {
            println('Gradle process can safely ignore stopTestDockerContainer task')
        }
    }
}

task stopTestLimitedDockerContainer() {
    doLast {
        try {
            def stdOut = new ByteArrayOutputStream()
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                exec {
                    commandLine 'cmd', '/c', 'docker stop ballerina-mysql-limited'
                    standardOutput = stdOut
                }
            } else {
                exec {
                    commandLine 'sh', '-c', 'docker stop ballerina-mysql-limited'
                    standardOutput = stdOut
                }
            }
        } catch (all) {
            println('Gradle process can safely ignore stopTestLimitedDockerContainer task')
        }
    }
}

task stopTestDockerTrxContainer() {
    doLast {
        try {
            def stdOut = new ByteArrayOutputStream()
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                exec {
                    commandLine 'cmd', '/c', 'docker stop ballerina-mysql-trx'
                    standardOutput = stdOut
                }
            } else {
                exec {
                    commandLine 'sh', '-c', 'docker stop ballerina-mysql-trx'
                    standardOutput = stdOut
                }
            }
        } catch (all) {
            println('Gradle process can safely ignore stopTestDockerTrxContainer task')
        }
    }
}

task stopTestSslDockerContainer() {
    doLast {
        try {
            def stdOut = new ByteArrayOutputStream()
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                exec {
                    commandLine 'cmd', '/c', 'docker stop ballerina-mysql-ssl'
                    standardOutput = stdOut
                }
            } else {
                exec {
                    commandLine 'sh', '-c', 'docker stop ballerina-mysql-ssl'
                    standardOutput = stdOut
                }
            }
        } catch (all) {
            println('Gradle process can safely ignore stopTestSslDockerContainer task')
        }
    }
}

updateTomlFiles.dependsOn copyStdlibs
startTestDockerContainer.dependsOn createTestDockerImage
startTestSslDockerContainer.dependsOn createTestDockerImage

build.dependsOn "generatePomFileForMavenPublication"
build.dependsOn ":${packageName}-native:build"
test.dependsOn ":${packageName}-native:build"
build.dependsOn ":${packageName}-compiler-plugin:build"
test.dependsOn ":${packageName}-compiler-plugin:build"
build.finalizedBy stopTestDockerContainer
build.finalizedBy stopTestLimitedDockerContainer
build.finalizedBy stopTestDockerTrxContainer
build.finalizedBy stopTestSslDockerContainer
test.dependsOn startTestDockerContainer
test.dependsOn startTestLimitedDockerContainer
test.dependsOn startTestDockerTrxContainer
test.dependsOn startTestSslDockerContainer
